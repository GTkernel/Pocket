# Plot all serial boot data.
# This graph is not for publication, just a quick sanity check to see all data in one graph
#
# invoke with:
# gnuplot -e "base='<dir>'" boot-serial-all.gpl
# where <dir> is the platform directory


fc_dat =        sprintf("%s/boot-serial-fc-api-cdf.dat", base)
fc_file_dat =   sprintf("%s/boot-serial-fc-file-cdf.dat", base)
fc_nopci_dat =  sprintf("%s/boot-serial-fc-nopci-api-cdf.dat", base)
fc_net_dat =    sprintf("%s/boot-serial-fc-net-api-cdf.dat", base)
chv_dat =       sprintf("%s/boot-serial-chv-cdf.dat", base)
chv_net_dat =   sprintf("%s/boot-serial-chv-net-cdf.dat", base)
chv_bz_dat =    sprintf("%s/boot-serial-chv-bz-cdf.dat", base)
qemu_dat =      sprintf("%s/boot-serial-qemu-cdf.dat", base)
qemu_uvm_dat =  sprintf("%s/boot-serial-qemu-uvm-cdf.dat", base)
qboot_dat =     sprintf("%s/boot-serial-qboot-cdf.dat", base)
qboot_net_dat = sprintf("%s/boot-serial-qboot-net-cdf.dat", base)

out_file = sprintf("%s/boot-serial-all.pdf", base)

set terminal pdf color solid enhanced
set output out_file

set xlabel 'Boot time (ms)'
set ylabel 'CDF'

set xrange [0:]
set yrange [0:1.05]
set offsets graph 0, 0, 0.01, 0.01

set key top left

set mytics 4
set grid ytics mytics xtics mxtics

plot \
     fc_dat          index 1 using 1:2         w l lw 2 t 'FC-pre', \
     fc_dat          index 0 using ($1/1000):2 w l lw 2 t 'FC', \
     fc_file_dat     index 0 using ($1/1000):2 w l lw 2 t 'FC-file', \
     fc_nopci_dat    index 1 using 1:2         w l lw 2 t 'FC-pre (noPCI kernel)', \
     fc_net_dat      index 1 using 1:2         w l lw 2 t 'FC+Network-pre', \
     fc_net_dat      index 0 using ($1/1000):2 w l lw 2 t 'FC+Network', \
     chv_dat         index 0 using ($1/1000):2 w l lw 2 t 'CloudHV', \
     chv_net_dat     index 0 using ($1/1000):2 w l lw 2 t 'CloudHV+Network', \
     chv_bz_dat      index 0 using ($1/1000):2 w l lw 2 t 'CloudHV (bzImage)', \
     qemu_dat        index 0 using ($1/1000):2 w l lw 2 t 'QEMU', \
     qemu_uvm_dat    index 0 using ($1/1000):2 w l lw 2 t 'QEMU microVM', \
     qboot_dat       index 0 using ($1/1000):2 w l lw 2 t 'QEMU qboot', \
     qboot_net_dat   index 0 using ($1/1000):2 w l lw 2 t 'QEMU qboot+Network', \

#     fc_lk_dat       index 1 using 1:2         w l lw 2 t 'FC-pre (w/ LK kernel)', \
