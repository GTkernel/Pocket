# Plot serial boot data for the apper
#
# invoke with:
# gnuplot -e "base='<dir>'" boot-serial.gpl
# where <dir> is the platform directory


fc_dat =    sprintf("%s/boot-serial-fc-api-cdf.dat", base)
chv_dat =   sprintf("%s/boot-serial-chv-cdf.dat", base)
qboot_dat = sprintf("%s/boot-serial-qboot-cdf.dat", base)

out_file = sprintf("%s/boot-serial.pdf", base)

set terminal pdf color solid enhanced font ',16' size 5,2.4
set output out_file

set xlabel 'Boot time (ms)'
set ylabel 'CDF'

set xrange [0:]
set yrange [0:1.05]
set offsets graph 0, 0, 0.01, 0.01

set key top left

set mytics 4
set grid ytics mytics xtics mxtics

plot \
     fc_dat     index 1 using 1:2         w l lw 2 t 'FC-pre', \
     fc_dat     index 0 using ($1/1000):2 w l lw 2 t 'FC', \
     chv_dat    index 0 using ($1/1000):2 w l lw 2 t 'CloudHV', \
     qboot_dat  index 0 using ($1/1000):2 w l lw 2 t 'QEMU', \
